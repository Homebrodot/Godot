#!/usr/bin/env python
Import("env")
files = [
    "os_vita.cpp",
    "godot_vita.cpp",
    "context_egl_vita.cpp",
    "joypad_vita.cpp",
    "audio_driver_vita.cpp",
]

prog = env.add_program("#bin/vita/godot", files)
build_target = env["target"]
env.Append(
    BUILDERS={
        "VitaELF": Builder(action=env["VITASDK_BIN"] + "vita-elf-create $SOURCE $TARGET"),
        "VitaEBOOT": Builder(action=env["VITASDK_BIN"] + "vita-make-fself -c -na $SOURCE $TARGET"),
    }
)
stripped_elf_path = "#bin/vita/vita_" + env["target"] + "_stripped"
env.Command(
    stripped_elf_path, prog, [Copy("$TARGET", "$SOURCE"), env["VITASDK_BIN"] + "arm-vita-eabi-strip -g $TARGET"]
)
env.VitaELF("#bin/vita/vita_" + env["target"] + ".velf", stripped_elf_path)
env.VitaEBOOT("#bin/vita/eboot.bin", "#bin/vita/vita_" + env["target"] + ".velf")

env.Command(
    "#bin/vita/vita_release/eboot.bin",
    "#bin/vita/eboot.bin",
    [
        Copy("$TARGET", "$SOURCE"),
    ],
)
env.Command(
    "#bin/vita/vita_release/",
    "#platform/vita/app/",
    [
        Copy("$TARGET", "$SOURCE"),
    ],
)
env.Command(
    "#bin/vita_release.zip",
    "#bin/vita/vita_release",
    [
        "cd $SOURCE && zip -r ../../$TARGET .",
    ],
)
