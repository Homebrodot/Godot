#!/usr/bin/env python

import version

Import("env")

files = [
    "godot_switch.cpp",
    "os_switch.cpp",
    "power_switch.cpp",
    "joypad_switch.cpp",
    "audio_driver_switch.cpp",
    "context_gl_switch_egl.cpp",
]

version_str = "{}.{}.{}-{}".format(version.major, version.minor, version.patch, version.status)
build_target = env["target"]
bin_name = "switch_" + build_target
nacp_name = bin_name + ".nacp"
nro_name = bin_name + ".nro"

# Build the engine
prog = env.Program("#temp-build/godot", files)
env.Command(
    "#temp-build/switch_" + build_target + ".nro",
    prog,
    env["DKP_TOOLS_BIN"] + "/elf2nro $SOURCE $TARGET --icon=platform/switch/icon.jpg "
    "--nacp=platform/switch/" + nacp_name + " --romfsdir=platform/switch/romfs",
)
env.Depends(target="#temp-build/switch_" + nro_name, dependency=nacp_name)

env.Command(
    None, None,
    action=env["DKP_TOOLS_BIN"] + "/nacptool --create 'Godot Engine' "
                                  "'Homebrodot Contributors' " + version_str + " " + nacp_name
)

# Build the forwarder
fwd_prog = env.Program("#temp-build/forwarder", ["forwarder/main.c"])
env.Command(
    "#temp-build/switch-forwarder.nro",
    fwd_prog,
    env["DKP_TOOLS_BIN"] + "/elf2nro $SOURCE $TARGET --icon=platform/switch/icon.jpg "
    "--nacp=platform/switch/forwarder/forwarder.nacp",
)
env.Depends(target="#temp-build/godot-forwarder.nro", dependency="forwarder/forwarder.nacp")

env.Command(
    None, None,
    action=env["DKP_TOOLS_BIN"] + "/nacptool --create 'Godot Forwarder' "
                                  "'Homebrodot Contributors' " + version_str + " forwarder/.nacp"
)

# Make the release and clean up
env.Command(
    "#temp-build", None,
    "cd $TARGET && zip -rd ../bin/switch_" + build_target + ".zip .",
)

# if someone needs this they can enable it themselves..
# env.Command("#bin/switch_" + env['target'] + ".nso", prog, "/opt/devkitpro/tools/bin/elf2nso $SOURCE $TARGET")
